import org.springframework.boot.gradle.plugin.SpringBootPlugin
import io.franzbecker.gradle.lombok.task.DelombokTask


plugins {
    id 'org.springframework.boot' version '2.6.7' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'groovy'
    id 'maven-publish'
    id 'java-library'
    id "io.franzbecker.gradle-lombok" version "5.0.0"

}

group = 'no.fintlabs'

sourceCompatibility = '11'
targetCompatibility = '11'


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://repo.fintlabs.no/releases"
    }
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

lombok { // optional: values below are the defaults
    version = "1.18.24"
}

task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = false
    title("FINT Core Infrastructure Models")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'org.jetbrains:annotations:23.0.0'

    implementation 'no.fint:fint-model-resource:0.4.1'

    //compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    //annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'cglib:cglib-nodep:3.3.0'
    testImplementation 'org.spockframework:spock-spring:2.1-M2-groovy-3.0'
    testImplementation 'org.spockframework:spock-core:2.1-M2-groovy-3.0'

}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

apply from: 'https://raw.githubusercontent.com/FINTLabs/fint-buildscripts/master/reposilite.ga.gradle'
